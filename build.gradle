plugins {
    id 'java-library'
    id 'groovy'
    id 'maven-publish'
}

group = 'au.djac'
version = '1.0.0'

repositories {
    mavenCentral()
}

dependencies {
    // SLF4J for logging
    implementation 'org.slf4j:slf4j-api:2.0.0'
    testImplementation 'ch.qos.logback:logback-classic:1.3.0'

    implementation 'org.fusesource.jansi:jansi:2.4.0'

//     // Testing with Spock
//     testImplementation 'org.codehaus.groovy:groovy:3.0.15'
//     testImplementation 'org.spockframework:spock-core:2.2-groovy-3.0'
//     testImplementation 'junit:junit:4.13.2'
//     testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(11)
    }
    withJavadocJar()
    withSourcesJar()
}

compileJava {
    options.compilerArgs << "-Xlint:all,-serial"
}

tasks.named('test') {
    useJUnitPlatform()
}

javadoc {
    options {
        links 'https://docs.oracle.com/en/java/javase/11/docs/api/'
    }
}

publishing {
    publications {
        TreeWriterLibrary(MavenPublication) {
            from components.java

            pom {
                name = 'TreeWriter'
                description = 'A library for outputting tree structures in Unicode/terminal environments'
                url = 'https://github.com/davidjacooper/treewriter'

                licenses {
                    license {
                        name = 'MIT License'
                        url = 'http://www.opensource.org/licenses/mit-license.php'
                    }
                }

                developers {
                    developer {
                        id = 'djac'
                        name = 'David J A Cooper'
                        email = 'david.cooper+treewriter@djac.au'
                    }
                }

                scm {
                    connection = 'scm:git:git://github.com/davidjacooper/treewriter.git'
                    developerConnection = 'scm:git:ssh://github.com/davidjacooper/treewriter.git'
                    url = 'https://github.com/davidjacooper/treewriter'
                }
            }
        }
    }

    repositories {
        maven {
            name = 'localRepo'
            url = layout.buildDirectory.dir('repo')
        }
    }
}
