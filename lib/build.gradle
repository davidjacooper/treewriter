plugins {
    id 'java-library'
    id 'groovy'
    id 'pmd'
    id 'maven-publish'
    id 'signing'
}

group = 'au.djac'
version = '1.0.0'

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.fusesource.jansi:jansi:2.4.1'
    testImplementation 'org.spockframework:spock-core:2.3-groovy-4.0'
    testImplementation 'org.apache.groovy:groovy:4.0.22'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(11)
    }
    withJavadocJar()
    withSourcesJar()
}

tasks.withType(JavaCompile) {
    options.compilerArgs << '-Xlint:all,-serial'
}

tasks.withType(Test) {
    useJUnitPlatform()
    testLogging {
        exceptionFormat "full"
        events "failed", "passed", "skipped"
        showStandardStreams = true
    }
}

pmd {
    consoleOutput = true
    toolVersion = '7.3.0'
    rulesMinimumPriority = 5
    ruleSetFiles = files('../pmd-ruleset.xml')
}


tasks.withType(Javadoc) {
    options {
        links 'https://docs.oracle.com/en/java/javase/11/docs/api/'
    }
}

publishing {
    publications {
        TreeWriterLibrary(MavenPublication) {
            artifactId = 'treewriter'
            from components.java

            // versionMapping { ... } ??

            pom {
                name = 'TreeWriter'
                description = 'A library for outputting tree structures in Unicode/terminal environments'
                url = 'https://github.com/davidjacooper/treewriter'

                licenses {
                    license {
                        name = 'MIT License'
                        url = 'http://www.opensource.org/licenses/mit-license.php'
                    }
                }

                developers {
                    developer {
                        id = 'djac'
                        name = 'David J A Cooper'
                        email = 'david.cooper+treewriter@djac.au'
                    }
                }

                scm {
                    connection = 'scm:git:git://github.com/davidjacooper/treewriter.git'
                    developerConnection = 'scm:git:ssh://github.com/davidjacooper/treewriter.git'
                    url = 'https://github.com/davidjacooper/treewriter'
                }
            }
        }
    }

    repositories {
        maven {
            name = 'localRepo'
            url = layout.buildDirectory.dir('repo')
        }
//         maven {
//             name = 'GitHubPackages'
//             url = 'https://maven.pkg.github.com/davidjacooper/treewriter'
//             credentials {
//                 username = System.getenv('GITHUB_USERNAME')
//                 password = System.getenv('GITHUB_TOKEN')
//             }
//         }
    }
}

signing {
    useGpgCmd()
    sign publishing.publications.TreeWriterLibrary
}

